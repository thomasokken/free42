apply plugin: 'com.android.application'

android {
    signingConfigs {
        release {
            storeFile file(free42_keystore)
            storePassword free42_keystorePassword
            keyAlias free42_key
            keyPassword free42_keyPassword
        }
    }
    compileSdkVersion 31
    defaultConfig {
        minSdkVersion 14
        targetSdkVersion 31
        versionCode 155
        versionName "3.0.20"
    }
    buildTypes {
        debug {
            ndk {
                abiFilters 'armeabi-v7a', 'arm64-v8a', 'x86', 'x86_64'
            }
            signingConfig signingConfigs.release
        }
        release {
            ndk {
                abiFilters 'armeabi-v7a', 'arm64-v8a'
            }
            minifyEnabled false
            signingConfig signingConfigs.release
        }
    }
    ndkVersion "19.2.5345600"
    externalNativeBuild {
        ndkBuild {
            path "src/main/cpp/Android.mk"
        }
    }
    flavorDimensions "floatType"
    productFlavors {
        decimal {
            getIsDefault().set(true)
            dimension "floatType"
            applicationId "com.thomasokken.free42"
            manifestPlaceholders = [appName:"Free42"]
            externalNativeBuild {
                ndkBuild {
                    arguments "FLOAT_TYPE=decimal"
                }
            }
        }
        binary {
            dimension "floatType"
            applicationId "com.thomasokken.free42b"
            manifestPlaceholders = [appName:"Free42 Binary"]
            externalNativeBuild {
                ndkBuild {
                    arguments "FLOAT_TYPE=binary"
                }
            }
        }
    }
    namespace 'com.thomasokken.free42'
}

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])
}
